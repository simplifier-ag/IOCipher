buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.library'

dependencies {
    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'net.zetetic:android-database-sqlcipher:4.2.0@aar'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        testApplicationId "info.guardianproject.iocipher.tests"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testHandleProfiling true
        testFunctionalTest true
    }

    // both libs/ and 'net.zetetic:android-database-sqlcipher:3.5.4@aar' contain these:
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libsqlcipher.so'
        pickFirst 'lib/arm64-v8a/libsqlcipher.so'
        pickFirst 'lib/x86/libsqlcipher.so'
        pickFirst 'lib/x86_64/libsqlcipher.so'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest {
            manifest.srcFile 'androidTest/AndroidManifest.xml'
            java.srcDirs = ['androidTest/src']
            resources.srcDirs = ['androidTest/src']
            aidl.srcDirs = ['androidTest/src']
            renderscript.srcDirs = ['androidTest/src']
            res.srcDirs = ['androidTest/res']
            assets.srcDirs = ['androidTest/assets']
        }
    }

    // before working here, run this in terminal: `make -C external`
    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
        }
    }

    task makeExternal(type: Exec) {
        commandLine 'make', '-C', file('external').absolutePath
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn makeExternal
    }

    task makeExternalClean(type: Exec) {
        commandLine 'make', '-C', file('external').absolutePath, 'clean'
    }

    lintOptions {
        abortOnError true

        htmlReport false
        xmlReport false
        textReport true

        error 'AppCompatMethod', 'NestedScrolling', 'StringFormatCount', 'UnsafeProtectedBroadcastReceiver'
    }
}

apply plugin: 'maven'

uploadArchives {
        repositories.mavenDeployer {
                def deployPath = file("mavenbuild")
                repository(url: "file://${deployPath.absolutePath}")
                pom.project {
                        groupId 'info.guardianproject.iocipher'
                        artifactId 'IOCipher'
                        version '0.4.2.1'
                }
        }
}
